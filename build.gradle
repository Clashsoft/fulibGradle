plugins {
	id 'java-gradle-plugin'
	id 'groovy'
	id 'com.gradle.plugin-publish' version '0.11.0'
}

apply from: 'gradle/integration-test.gradle'
apply from: 'gradle/functional-test.gradle'

group 'org.fulib'
version 'git describe --tags'.execute().text[1..-2] // strip v and \n
description 'The Fulib Gradle Plugin.'

pluginBundle {
	website = 'https://github.com/fujaba/fulibGradle'
	vcsUrl = 'https://github.com/fujaba/fulibGradle'
	description = project.description
	tags = [ 'generate', 'fulib' ]

	plugins {
		fulibGradle {
			displayName = 'fulib'
		}
	}

	mavenCoordinates {
		groupId = project.group
		artifactId = project.name
		version = project.version
	}
}

gradlePlugin {
	plugins {
		fulibGradle {
			id = "${project.group}.${project.name}"
			implementationClass = 'org.fulib.gradle.FulibGradlePlugin'
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// https://mvnrepository.com/artifact/junit/junit
	testImplementation group: 'junit', name: 'junit', version: '4.13.2'

	testImplementation(group: 'org.spockframework', name: 'spock-core', version: '2.0-M5-groovy-3.0') {
		exclude group: 'org.codehaus.groovy'
	}
}
